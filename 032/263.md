Cheery Cinnamon Rook

Medium

# The `SolidlyV2AMO::_mintAndSellBoost()` causes stuck BOOST tokens in the contract

### Summary

The `SolidlyV2AMO` contract provides a rebalancing mechanism to restore the depeg of the BOOST token for each BOOST-USD pool. If the BOOST is over the peg, the following functions can be called to restore the BOOST depeg to $1.
- `mintAndSellBoost()` -- `AMO_ROLE` access-restricted function
- `mintSellFarm()` -- `AMO_ROLE` access-restricted function
- `mintSellFarm()` -- `Public` access-restricted function

Under the hood, the above functions will execute the internal `_mintAndSellBoost()` to mint free BOOST tokens and sell them for USD tokens, returning the BOOST token's price to peg with $1. However, I discovered that some minted BOOST tokens can be unspent and stuck in the contract during this process.

### Root Cause

The snippet below presents the internal `SolidlyV2AMO::_mintAndSellBoost()`. This function will be invoked when the BOOST token is over the peg. The function will [mint free BOOST tokens](https://github.com/sherlock-audit/2024-10-axion/blob/d75df3636ea28dd627d548d5d473c5d5477b0dc6/liquidity-amo/contracts/SolidlyV2AMO.sol#L162) (see `@1`) and then [approve the transfer of BOOST tokens](https://github.com/sherlock-audit/2024-10-axion/blob/d75df3636ea28dd627d548d5d473c5d5477b0dc6/liquidity-amo/contracts/SolidlyV2AMO.sol#L165) to the `router` (see `@2`).

Later, the function will execute the `router` to [swap the minted BOOST tokens for the pool-specified USD tokens](https://github.com/sherlock-audit/2024-10-axion/blob/d75df3636ea28dd627d548d5d473c5d5477b0dc6/liquidity-amo/contracts/SolidlyV2AMO.sol#L175-L181) (see `@3`). However, due to several factors on-chain -- such as price impact, price slippage, rounding issues, etc. -- some minted BOOST tokens will typically be unspent (this is the nature of token swaps).

I noticed that the `_mintAndSellBoost()` does not burn the unspent BOOST tokens, gradually causing them to become stuck in the `SolidlyV2AMO` contract. Furthermore, the function does not revoke the residual approval from the `router` (external entity).

```solidity
    function _mintAndSellBoost(
        uint256 boostAmount,
        uint256 minUsdAmountOut,
        uint256 deadline
    ) internal override returns (uint256 boostAmountIn, uint256 usdAmountOut) {
        // Mint the specified amount of BOOST tokens
@1      IMinter(boostMinter).protocolMint(address(this), boostAmount);

        // Approve the transfer of BOOST tokens to the router
@2      IERC20Upgradeable(boost).approve(router, boostAmount);

        // Define the route to swap BOOST tokens for USD tokens
        ISolidlyRouter.route[] memory routes = new ISolidlyRouter.route[](1);
        routes[0] = ISolidlyRouter.route(boost, usd, true);

        if (minUsdAmountOut < toUsdAmount(boostAmount)) minUsdAmountOut = toUsdAmount(boostAmount);

        uint256 usdBalanceBefore = balanceOfToken(usd);
        // Execute the swap and store the amounts of tokens involved
@3      uint256[] memory amounts = ISolidlyRouter(router).swapExactTokensForTokens(
@3          boostAmount,
@3          minUsdAmountOut,
@3          routes,
@3          address(this),
@3          deadline
@3      );
        uint256 usdBalanceAfter = balanceOfToken(usd);
        boostAmountIn = amounts[0];
        usdAmountOut = amounts[1];

        // we check that selling BOOST yields proportionally more USD
        if (usdAmountOut != usdBalanceAfter - usdBalanceBefore)
            revert UsdAmountOutMismatch(usdAmountOut, usdBalanceAfter - usdBalanceBefore);

        if (usdAmountOut < minUsdAmountOut) revert InsufficientOutputAmount(usdAmountOut, minUsdAmountOut);

        emit MintSell(boostAmount, usdAmountOut);
    }
```

- `@1`: https://github.com/sherlock-audit/2024-10-axion/blob/d75df3636ea28dd627d548d5d473c5d5477b0dc6/liquidity-amo/contracts/SolidlyV2AMO.sol#L162

- `@2`: https://github.com/sherlock-audit/2024-10-axion/blob/d75df3636ea28dd627d548d5d473c5d5477b0dc6/liquidity-amo/contracts/SolidlyV2AMO.sol#L165

- `@3`: https://github.com/sherlock-audit/2024-10-axion/blob/d75df3636ea28dd627d548d5d473c5d5477b0dc6/liquidity-amo/contracts/SolidlyV2AMO.sol#L175-L181

### Internal pre-conditions

When one of the following functions is executed (by `AMO_ROLE` accounts or any `public` users) to restore the BOOST depeg to $1.
- `SolidlyV2AMO::mintAndSellBoost()` -- `AMO_ROLE` access-restricted function
- `SolidlyV2AMO::mintSellFarm()` -- `AMO_ROLE` access-restricted function
- `SolidlyV2AMO::mintSellFarm()` -- `Public` access-restricted function

### External pre-conditions

When the BOOST price for each BOOST-USD pool is over the peg, it is highly likely to happen.

### Attack Path

1. The BOOST price for a specific BOOST-USD pool is over the peg (it is highly likely to happen).
2. An `AMO_ROLE` account or `public` user executes the `mintAndSellBoost()` or `mintSellFarm()` function to restore the BOOST depeg to $1.
	1. The internal `_mintAndSellBoost()` will be invoked to mint free BOOST tokens and sell them for USD tokens.
	2. Over time, the unspent BOOST tokens will gradually become stuck in the `SolidlyV2AMO` contract (for each pool).

*Note: as previously described in the `Root Cause` section, the `_mintAndSellBoost()` also does not revoke the residual approval from the `router` (external entity). This can also lead to the thief of stuck BOOST tokens via the `router` (in case the `router` is somehow compromised).*

### Impact

In this report, there are two primary impact points:

1. The `_mintAndSellBoost()` does not burn the unspent BOOST tokens, which gradually causes them to become stuck in the `SolidlyV2AMO` contract. This can occur in all BOOST-USD pools. The stuck tokens can eventually dilute the value of the BOOST tokens, which the protocol's users and partners will hold over time. 
   
   Furthermore, even though the contract would implement the [`withdrawERC20()`](https://github.com/sherlock-audit/2024-10-axion/blob/c65e662999d0c79439703fc6713814b4ad023e01/liquidity-amo/contracts/MasterAMO.sol#L312-L319) with the `WITHDRAWER_ROLE` access restriction and the [`getReward()`](https://github.com/sherlock-audit/2024-10-axion/blob/d75df3636ea28dd627d548d5d473c5d5477b0dc6/liquidity-amo/contracts/SolidlyV2AMO.sol#L320-L339) with the `REWARD_COLLECTOR_ROLE` access restriction *(Note: the `getReward()` can also sweep all stuck BOOST tokens if the token is whitelisted as a reward token)*. That is, the stuck BOOST tokens can be withdrawn and dumped by malicious `WITHDRAWER_ROLE` or `REWARD_COLLECTOR_ROLE` actors ***(Note: Very LOW likelihood as the `WITHDRAWER_ROLE` and `REWARD_COLLECTOR_ROLE` are trusted)***. Nevertheless, this can lead to trust and transparency issues with the BOOST token, directly affecting the `AXION` protocol.

2. The `_mintAndSellBoost()` does not revoke the residual approval from the `router` (external entity). This can also lead to the thief of stuck BOOST tokens via the `router` (in case the `router` is somehow compromised).

### PoC

-

### Mitigation

Burn the unspent free-minted BOOST tokens and revoke the `router`'s residual approval, as in the code snippet below.

```diff
    function _mintAndSellBoost(
        uint256 boostAmount,
        uint256 minUsdAmountOut,
        uint256 deadline
    ) internal override returns (uint256 boostAmountIn, uint256 usdAmountOut) {
        // Mint the specified amount of BOOST tokens
        IMinter(boostMinter).protocolMint(address(this), boostAmount);

        // Approve the transfer of BOOST tokens to the router
        IERC20Upgradeable(boost).approve(router, boostAmount);

        // Define the route to swap BOOST tokens for USD tokens
        ISolidlyRouter.route[] memory routes = new ISolidlyRouter.route[](1);
        routes[0] = ISolidlyRouter.route(boost, usd, true);

        if (minUsdAmountOut < toUsdAmount(boostAmount)) minUsdAmountOut = toUsdAmount(boostAmount);

        uint256 usdBalanceBefore = balanceOfToken(usd);
        // Execute the swap and store the amounts of tokens involved
        uint256[] memory amounts = ISolidlyRouter(router).swapExactTokensForTokens(
            boostAmount,
            minUsdAmountOut,
            routes,
            address(this),
            deadline
        );
        uint256 usdBalanceAfter = balanceOfToken(usd);
        boostAmountIn = amounts[0];
        usdAmountOut = amounts[1];

+       // Burn the unspent free-minted BOOST tokens and revoke the router's residual approval
+       if (boostAmount > boostAmountIn) {
+           IBoostStablecoin(boost).burn(boostAmount - boostAmountIn); // Burn
+           IERC20Upgradeable(boost).approve(router, 0); // Revoke
+       }

        // we check that selling BOOST yields proportionally more USD
        if (usdAmountOut != usdBalanceAfter - usdBalanceBefore)
            revert UsdAmountOutMismatch(usdAmountOut, usdBalanceAfter - usdBalanceBefore);

        if (usdAmountOut < minUsdAmountOut) revert InsufficientOutputAmount(usdAmountOut, minUsdAmountOut);

        emit MintSell(boostAmount, usdAmountOut);
    }
```