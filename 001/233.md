Fast Lemonade Hawk

Medium

# SolidlyV2AMO `boostPrice()` is inaccurate.


### Summary

The current implementation to calculate `boostPrice()` for SolidlyV2AMO is inaccurate due to the existance of swap fees. This may cause price checks to be inaccurate.

### Root Cause

The current implementation for calculating `boostPrice()` is calculate the amount of output USD tokens when given 1e18 Boost tokens, and dividing the token amounts.

However, the function for SolidlyV2 pools is `getAmountOut()`, which is actually used for swaps, and it would incur a swap fee.

This means the returned boost price is always lower than the spot price (which would be the two token's pool reserves amount dividing each other). For example, if the fee is 1%, if the spot price is 1, the `boostPrice()` would return 0.99.

```solidity
    function boostPrice() public view override returns (uint256 price) {
        uint256 amountOut = IPair(pool).getAmountOut(10 ** boostDecimals, boost);
        price = amountOut / 10 ** (usdDecimals - PRICE_DECIMALS);
    }
```

Aerodrome (SolidlyV2 fork) implementation for `getAmountOut`.

```solidity
    function getAmountOut(uint256 amountIn, address tokenIn) external view returns (uint256) {
        (uint256 _reserve0, uint256 _reserve1) = (reserve0, reserve1);
        amountIn -= (amountIn * IPoolFactory(factory).getFee(address(this), stable)) / 10000; // remove fee from amount received
        return _getAmountOut(amountIn, tokenIn, _reserve0, _reserve1);
    }
```

- https://github.com/sherlock-audit/2024-10-axion/blob/main/liquidity-amo/contracts/SolidlyV2AMO.sol#L390-L393
- https://github.com/aerodrome-finance/contracts/blob/main/contracts/Pool.sol#L454

### Internal pre-conditions

N/A

### External pre-conditions

1. Swap fees enabled.

### Attack Path

N/A

### Impact

Having an inaccurate `boostPrice()` may harm multiple places that use this price, for example:

1. In `mintSellFarm`, the addLiquidity is performed only when the boost price within a range.

```solidity
        uint256 price = boostPrice();
        if (price > FACTOR - validRangeWidth && price < FACTOR + validRangeWidth) {
            uint256 usdBalance = IERC20Upgradeable(usd).balanceOf(address(this));
            (boostSpent, usdSpent, liquidity) = _addLiquidity(usdBalance, minBoostSpend, minUsdSpend, deadline);
        }
```

- https://github.com/sherlock-audit/2024-10-axion/blob/main/liquidity-amo/contracts/MasterAMO.sol#L211

2. In the public `unfarmBuyBurn()` and `mintSellFarm()` functions, the final boost price is checked against a limit to make sure the price is in a valid range.

```solidity
    function mintSellFarm()
        external
        override
        whenNotPaused
        nonReentrant
        validateSwap(SELL_BOOST)
        returns (uint256 liquidity, uint256 newBoostPrice)
    {
        (liquidity, newBoostPrice) = _mintSellFarm(); // Perform the mint and sell, and return liquidity and the new Boost price
        // Checks if the actual average price of boost when selling is greater than the boostLowerPriceSell
@>       if (newBoostPrice < boostLowerPriceSell) revert PriceNotInRange(newBoostPrice);
        emit PublicMintSellFarmExecuted(liquidity, newBoostPrice);
    }

    // This function handles the un-farming, buying, and burning of Boost when it's under the peg.
    function unfarmBuyBurn()
        external
        override
        whenNotPaused // Ensures the contract is not paused
        nonReentrant
        validateSwap(BUY_BOOST)
        returns (uint256 liquidity, uint256 newBoostPrice)
    {
        (liquidity, newBoostPrice) = _unfarmBuyBurn();
        // Checks if the actual average price of boost when buying is less than the boostUpperPriceBuy
@>      if (newBoostPrice > boostUpperPriceBuy) revert PriceNotInRange(newBoostPrice);

        emit PublicUnfarmBuyBurnExecuted(liquidity, newBoostPrice);
    }
```

- https://github.com/sherlock-audit/2024-10-axion/blob/main/liquidity-amo/contracts/MasterAMO.sol#L278-L309

### PoC

N/A

### Mitigation

Calculate the boost spot price by dividing the reserve token amounts for an accurate price.