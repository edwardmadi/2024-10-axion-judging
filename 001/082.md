Jolly Basil Boar

Medium

# Lack of Slippage Protection and Short Deadline in _mintSellFarm Function

### Summary

The `_mintSellFarm` functions in the protocol (`SolidlyV2AMO.sol` and `SolidlyV3AMO.sol`) and `SolidlyV3AMO.sol::_unfarmBuyBurn` lack proper slippage protection and use `block.timestamp` as a deadline, which exposes the protocol to unfavorable price fluctuations during the swap of `BOOST` tokens for `USD`. By setting `minBoostSpend` and `minUsdSpend` to `1`, the function allows swaps to be executed with virtually no safeguard against price slippage. Additionally, the deadline set to `block.timestamp + 1` can be manipulated:

https://github.com/sherlock-audit/2024-10-axion/blob/main/liquidity-amo/contracts/SolidlyV2AMO.sol#L347-L353

https://github.com/sherlock-audit/2024-10-axion/blob/main/liquidity-amo/contracts/SolidlyV3AMO.sol#L308-L314

https://github.com/sherlock-audit/2024-10-axion/blob/main/liquidity-amo/contracts/SolidlyV3AMO.sol#L328-L334

Rest is written only for `_mintSellFarm()` function of the `SolidlyV2AMO` as the same goes for the `SolidlyV3AMO` contract also.

### Root Cause

The use of `1` as the minimum values for `minBoostSpend` and `minUsdSpend`, along with a deadline of `block.timestamp + 1`, fails to provide adequate protection against price fluctuations during swaps.

### Internal pre-conditions

1. The function `_mintSellFarm` is called with the intention to swap `BOOST` tokens for `USD` tokens.
2. The `minBoostSpend` and `minUsdSpend` are hardcoded to `1`, effectively allowing any amount of tokens to be swapped without proper checks.
3. The deadline is set to `block.timestamp + 1`, which can be manipulated by the m iners..

### External pre-conditions

1. The liquidity pool for `BOOST-USD` is susceptible to price fluctuations.
2. There is a possibility of miners manipulating the `block.timestamp` for doing sandwich attacks.

### Attack Path

1. The user calls public `MasterAMO.sol::mintSellFarm()` function:
```solidity
    function mintSellFarm()
        external
        override
        whenNotPaused
        nonReentrant
        validateSwap(SELL_BOOST)
        returns (uint256 liquidity, uint256 newBoostPrice)
    {
        (liquidity, newBoostPrice) = _mintSellFarm(); // Perform the mint and sell, and return liquidity and the new Boost price
        // Checks if the actual average price of boost when selling is greater than the boostLowerPriceSell
        if (newBoostPrice < boostLowerPriceSell) revert PriceNotInRange(newBoostPrice);

        emit PublicMintSellFarmExecuted(liquidity, newBoostPrice);
    }
```
2. which calls internal `SolidlyV2AMO.sol::_mintSellFarm()`:
```solidity
    function _mintSellFarm() internal override returns (uint256 liquidity, uint256 newBoostPrice) {
        (uint256 boostReserve, uint256 usdReserve) = getReserves();

        uint256 boostAmountIn = (((usdReserve - boostReserve) / 2) * boostSellRatio) / FACTOR;

        (, , , , liquidity) = _mintSellFarm(
            boostAmountIn,
            toUsdAmount(boostAmountIn), // minUsdAmountOut
            1, // minBoostSpend
            1, // minUsdSpend
            block.timestamp + 1 // deadline
        );

        newBoostPrice = boostPrice();
    }
```
3. then the `SolidlyV2AMO.sol::_mintSellFarm()` calls internal `MasterAMO.sol::_mintSellFarm()` with `minBoostSpend` and `minUsdSpend` as `1`:
```solidity
    function _mintSellFarm(
        uint256 boostAmount,
        uint256 minUsdAmountOut,
        uint256 minBoostSpend,
        uint256 minUsdSpend,
        uint256 deadline
    )
        internal
        returns (uint256 boostAmountIn, uint256 usdAmountOut, uint256 boostSpent, uint256 usdSpent, uint256 liquidity)
    {
        (boostAmountIn, usdAmountOut) = _mintAndSellBoost(boostAmount, minUsdAmountOut, deadline);

        uint256 price = boostPrice();
        if (price > FACTOR - validRangeWidth && price < FACTOR + validRangeWidth) {
            uint256 usdBalance = IERC20Upgradeable(usd).balanceOf(address(this));
            (boostSpent, usdSpent, liquidity) = _addLiquidity(usdBalance, minBoostSpend, minUsdSpend, deadline);
        }
    }
```
which should call `_mintAndSellBoost()` and then do `_addLiquidity()`,
4. when calling `_addLiquidity()` with mentioned parameters set equal to 1, it is adding the liquidity through the router without slippage protection and `deadline` set as `block.timestamp + 1`:
```solidity
    function _addLiquidity(
        uint256 usdAmount,
        uint256 minBoostSpend,
        uint256 minUsdSpend,
        uint256 deadline
    ) internal override returns (uint256 boostSpent, uint256 usdSpent, uint256 liquidity) {
        // We only add liquidity when price is withing range (close to $1)
        // Price needs to be in range: 1 +- validRangeRatio / 1e6 == factor +- validRangeRatio
        // if price is too high, we need to mint and sell more before we add liqudiity
        uint256 price = boostPrice();
        if (price <= FACTOR - validRangeWidth || price >= FACTOR + validRangeWidth) revert InvalidRatioToAddLiquidity();

        // Mint the specified amount of BOOST tokens
        uint256 boostAmount = (toBoostAmount(usdAmount) * boostMultiplier) / FACTOR;

        IMinter(boostMinter).protocolMint(address(this), boostAmount);

        // Approve the transfer of BOOST and USD tokens to the router
        IERC20Upgradeable(boost).approve(router, boostAmount);
        IERC20Upgradeable(usd).approve(router, usdAmount);

        uint256 lpBalanceBefore = balanceOfToken(pool);
        // Add liquidity to the BOOST-USD pool
        (boostSpent, usdSpent, liquidity) = ISolidlyRouter(router).addLiquidity(
            boost,
            usd,
            true,
            boostAmount,
            usdAmount,
            minBoostSpend,
            minUsdSpend,
            address(this),
            deadline
        );
        uint256 lpBalanceAfter = balanceOfToken(pool);

        if (liquidity != lpBalanceAfter - lpBalanceBefore)
            revert LpAmountOutMismatch(liquidity, lpBalanceAfter - lpBalanceBefore);

        // Revoke approval from the router
        IERC20Upgradeable(boost).approve(router, 0);
        IERC20Upgradeable(usd).approve(router, 0);

        // Approve the transfer of liquidity tokens to the gauge and deposit them
        IERC20Upgradeable(pool).approve(gauge, liquidity);
        if (useTokenId) {
            IGauge(gauge).deposit(liquidity, tokenId);
        } else {
            IGauge(gauge).deposit(liquidity);
        }

        // Burn excessive boosts
        if (boostAmount > boostSpent) IBoostStablecoin(boost).burn(boostAmount - boostSpent);

        emit AddLiquidityAndDeposit(boostSpent, usdSpent, liquidity, tokenId);
    }
```

### Impact

The lack of slippage protection and manipulatable deadline could result in financial losses for the protocol, as the protocol can get fewer liquidity tokens then intended.

### PoC

_No response_

### Mitigation

At least introduce a configurable slippage tolerance. It can be calculated as done in the `_unfarmBuyBurn()`.