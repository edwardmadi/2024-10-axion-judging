Nutty Tweed Puppy

High

# Incorrect minUsdAmountOut Calculation Causes DoS, Preventing AMO Bot from Stabilizing BOOST-USD Peg

### Summary

The `mintAndSellBoost` function in the `SolidlyV2AMO` contract contains a critical vulnerability that prevents the successful execution of the function due to incorrect calculations of the minimum expected USD output. This flaw arises from an incorrect assumption regarding the 1:1 exchange rate between BOOST tokens and USD tokens, disregarding fees incurred during token swaps. As a result, the function always reverts with an `InsufficientOutputAmount` error, leading to a **complete Denial of Service (DoS)**. This makes the function entirely unusable, preventing the protocol from performing its intended stabilization role of bringing the BOOST price back to its peg when it diverges.

### Root Cause


The root cause of the issue lies in the calculation of `minUsdAmountOut` within the `_mintAndSellBoost` function ([SolidlyV2AMO.sol:L156](https://github.com/sherlock-audit/2024-10-axion/blob/main/liquidity-amo/contracts/SolidlyV2AMO.sol#L156)). When the function is called with a `minUsdAmountOut` of 0, the following condition is evaluated ([SolidlyV2AMO.sol:L171](https://github.com/sherlock-audit/2024-10-axion/blob/main/liquidity-amo/contracts/SolidlyV2AMO.sol#L171)):

```solidity
if (minUsdAmountOut < toUsdAmount(boostAmount)) minUsdAmountOut = toUsdAmount(boostAmount);
```

Here, `toUsdAmount(boostAmount)` calculates the expected USD amount based on the provided `boostAmount`. The method `toUsdAmount` is defined as follows in the `MasterAMO` contract ([MasterAMO.sol:L336](https://github.com/sherlock-audit/2024-10-axion/blob/main/liquidity-amo/contracts/MasterAMO.sol#L336)):

```solidity
function toUsdAmount(uint256 boostAmount) internal view returns (uint256) {
    return boostAmount / 10 ** (boostDecimals - usdDecimals);
}
```

Given `boostDecimals` of 18 and `usdDecimals` of 6, the calculation for a `boostAmount` of 1 (1 * 10^18) results in an expected USD amount of 1,000,000 USD. This value is then assigned to `minUsdAmountOut`, leading to the expectation that 1 BOOST token will yield 1 USD. However, since the actual token swap incurs a fee (e.g., 0.03% on the Equalizer DEX), the function will always revert when the swap results in a lower USD output due to the fee deduction. This creates a consistent failure in the function whenever it is called, leading to denial of service.

Moreover, during the execution of the swap, the following condition checks the actual output against the inflated `minUsdAmountOut` ([SolidlyV2AMO.sol#L190](https://github.com/sherlock-audit/2024-10-axion/blob/main/liquidity-amo/contracts/SolidlyV2AMO.sol#L190)):

```solidity
if (usdAmountOut < minUsdAmountOut) revert InsufficientOutputAmount(usdAmountOut, minUsdAmountOut);
```

This condition will always fail due to the inflated expectations set earlier in the function.


### Internal pre-conditions

- The contract must be initialized correctly.
- The caller must have the AMO role (`onlyRole(AMO_ROLE)`).
- The contract must not be paused (`whenNotPaused`).
- The transaction must be non-reentrant (`nonReentrant`).

### External pre-conditions

- The `IMinter` contract must be properly configured and accessible.
- The `ISolidlyRouter` must support the defined swap operations.

### Attack Path

1. An address with the AMO role calls the `mintAndSellBoost` function.
2. The contract mints a specified amount of BOOST tokens.
3. It attempts to approve and swap the minted BOOST tokens for USD tokens.
4. The calculation of `minUsdAmountOut` results in an inflated expectation of the output due to the incorrect handling of fees.
5. The swap transaction fails, resulting in the `InsufficientOutputAmount` error.


### Impact


The vulnerability in the `mintAndSellBoost` function not only disrupts its intended purpose of stabilizing the BOOST token price when it diverges from its peg, but it also results in a complete **Denial of Service (DoS)** for the function. The function is designed to mint BOOST and sell it for USD when BOOST is trading above $1, thereby bringing the price back down to peg. However, due to incorrect calculations of the minimum USD output, the function consistently reverts, making it impossible to execute.

The impact includes:

1. **Peg Instability**: The protocol is unable to mint and sell BOOST as required, leading to prolonged price deviations when BOOST is trading above $1. This inability to correct the price disrupts the stability of the token.

2. **Denial of Service (DoS)**: The function is entirely unusable, as it consistently reverts due to the erroneous USD output calculation. This effectively disables the system's primary mechanism for maintaining the BOOST token peg.

3. **Economic Inefficiency**: Without the ability to sell BOOST for USD, the system cannot manage liquidity or stabilize the token price effectively, leading to potential market imbalances and reduced investor confidence.

4. **Increased Volatility**: The failure to execute the stabilization mechanism can cause greater volatility in the BOOST price, potentially damaging the protocol's market perception and economic stability.


### PoC


```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

import {Test, console} from "forge-std/Test.sol";
import {MockERC20} from "./../src/mock/MockERC20.sol";
import {MockRouter} from "./../src/mock/MockRouter.sol";
import {BoostStablecoin} from "./../src/BoostStablecoin.sol";
import {Minter} from "../src/Minter.sol";
import {ProxyAdmin} from "@openzeppelin/contracts/proxy/transparent/ProxyAdmin.sol";
import {TransparentUpgradeableProxy} from "@openzeppelin/contracts/proxy/transparent/TransparentUpgradeableProxy.sol";
import {IFactory} from "./../src/interfaces/v2/IFactory.sol";
import {IPair} from "./../src/interfaces/v2/IPair.sol";
import {IV2Voter} from "./../src/interfaces/v2/IV2Voter.sol";
import {SolidlyV2AMO} from "./../src/SolidlyV2AMO.sol";

contract CounterTest is Test {
    // Declare mock tokens and contract addresses
    MockERC20 public usdToken;
    BoostStablecoin public boostToken;
    address public adminForBoostToken = makeAddr("adminForBoostToken");
    address public boostMinter = makeAddr("boostMinter");
    address public treasury = makeAddr("treasury");
    address public rewardVault = makeAddr("rewardVault");
    address public amoBot = makeAddr("AmoBot");
    Minter public minter_Contract;

    // Proxy for Boost token
    ProxyAdmin public ProxyAdminForBoostToken;
    TransparentUpgradeableProxy public TUproxyForBoostToken;

    // Proxy for Minter contract
    ProxyAdmin public ProxyAdminForMinter_con;
    TransparentUpgradeableProxy public TUproxyForMinter_con;

    // Factory address for creating the BOOST-USD pair on Equalizer DEX
    IFactory public V2_Factor;
    IPair public pool_addr_boost_usd;

    // Equalizer DEX voter to create gauge
    IV2Voter public V2_Voter;
    address public GuageAddress;

    // Router for adding liquidity and executing trades
    MockRouter public V2_Router;
    address public WETH = 0x21be370D5312f44cB42ce377BC9b8a0cEF1A4C83;

    // SolidlyV2AMO contract and its proxy
    SolidlyV2AMO public V2_AMO;
    ProxyAdmin public ProxyAdminForV2_AMO;
    TransparentUpgradeableProxy public TUproxyForV2_AMO;

    // Setup the test environment
    function setUp() public {
        // Deploy mock USD and Boost tokens
        usdToken = new MockERC20("usd token", "usd", 6);
        boostToken = new BoostStablecoin();

        // Set up proxy for Boost token
        ProxyAdminForBoostToken = new ProxyAdmin();
        TUproxyForBoostToken = new TransparentUpgradeableProxy(address(boostToken), address(ProxyAdminForBoostToken), "");

        // Initialize the Boost token contract
        BoostStablecoin(address(TUproxyForBoostToken)).initialize(adminForBoostToken);

        // Deploy and initialize the Minter contract
        minter_Contract = new Minter();
        ProxyAdminForMinter_con = new ProxyAdmin();
        TUproxyForMinter_con = new TransparentUpgradeableProxy(address(minter_Contract), address(ProxyAdminForMinter_con), "");

        // Assign MINTER_ROLE to Minter contract and Boost minter
        vm.startPrank(adminForBoostToken);
        BoostStablecoin(address(TUproxyForBoostToken)).grantRole(boostToken.MINTER_ROLE(), address(TUproxyForMinter_con)); 
        BoostStablecoin(address(TUproxyForBoostToken)).grantRole(boostToken.MINTER_ROLE(), address(boostMinter));     
        vm.stopPrank();

        // Initialize the Minter contract with the Boost and USD token addresses
        Minter(address(TUproxyForMinter_con)).initialize(address(TUproxyForBoostToken), address(usdToken), adminForBoostToken);

        // Mint some initial Boost and USD tokens to admin
        vm.startPrank(boostMinter);  
        BoostStablecoin(address(TUproxyForBoostToken)).mint(adminForBoostToken, 10000e18); // Mint 10,000 BOOST
        usdToken.mint(adminForBoostToken, 10000e6); // Mint 10,000 USD
        vm.stopPrank();

        // Get factory address from Equalizer DEX
        vm.startPrank(adminForBoostToken);
        V2_Factor = IFactory(0xc6366EFD0AF1d09171fe0EBF32c7943BB310832a); // Factory address
        vm.stopPrank();

        // Create a pair (BOOST-USD) in the factory
        pool_addr_boost_usd = IPair(V2_Factor.createPair(address(TUproxyForBoostToken), address(usdToken), true));

        // Create a gauge for the liquidity pool using the voter contract
        V2_Voter = IV2Voter(0xE3D1A117dF7DCaC2eB0AC8219341bAd92f18dAC1);
        vm.startPrank(V2_Voter.governor());
        GuageAddress = V2_Voter.createGauge(address(pool_addr_boost_usd));
        vm.stopPrank();

        // Deploy and set up router for adding liquidity
        vm.startPrank(adminForBoostToken);
        V2_Router = new MockRouter(address(V2_Factor), WETH); // MockRouter for testing
        vm.stopPrank();

        // Deploy SolidlyV2AMO contract and its proxy
        V2_AMO = new SolidlyV2AMO();
        ProxyAdminForV2_AMO = new ProxyAdmin();
        TUproxyForV2_AMO = new TransparentUpgradeableProxy(address(V2_AMO), address(ProxyAdminForV2_AMO), "");

        // Grant AMO_ROLE to the SolidlyV2AMO contract
        Minter(address(TUproxyForMinter_con)).grantRole(minter_Contract.AMO_ROLE(), address(TUproxyForV2_AMO));
    }

    // Test function for initializing the AMO contract
    function initailization() public {
        SolidlyV2AMO(address(TUproxyForV2_AMO)).initialize(adminForBoostToken,
            address(TUproxyForBoostToken), 
            address(usdToken), 
            address(TUproxyForMinter_con), 
            address(V2_Router), 
            GuageAddress, 
            rewardVault, 
            0, 
            false, 
            1100000, // boostMultiplier 1.1e6
            10000,   // validRangeWidth 0.01e6
            1010000, // validRemovingRatio 1.01e6
            990000,  // boostLowerPriceSell 0.99e6
            1010000, // boostUpperPriceBuy 1.01e6
            800000,  // boostSellRatio 0.8e6
            800000   // usdBuyRatio 0.8e6
        );
    }

    // Test function for the mintAndSellBoost vulnerability
    function test_mintAndSellBoost() public {
        initailization();

        // Add liquidity to the BOOST-USD pool
        vm.startPrank(adminForBoostToken);
        usdToken.approve(address(V2_Router), 10000e6); // Approve 10,000 USD
        BoostStablecoin(address(TUproxyForBoostToken)).approve(address(V2_Router), 10000e18); // Approve 10,000 BOOST
        V2_Router.addLiquidity(address(usdToken), address(TUproxyForBoostToken), true, 10000e6, 10000e18, 0, 0, address(TUproxyForV2_AMO), block.timestamp);
        console.log(SolidlyV2AMO(address(TUproxyForV2_AMO)).boostPrice()); // Log the BOOST price for debugging
        SolidlyV2AMO(address(TUproxyForV2_AMO)).grantRole(V2_AMO.AMO_ROLE(), amoBot); // Grant AMO role to amoBot
        vm.stopPrank();
         
        // Simulate minting and selling BOOST with AMO bot
        vm.startPrank(amoBot);
        // This call is expected to fail due to incorrect handling of minUsdAmountOut
        SolidlyV2AMO(address(TUproxyForV2_AMO)).mintAndSellBoost(1e18, 0, block.timestamp); // Vulnerable function
        vm.stopPrank();
    }
}
```


### Mitigation

To resolve this issue, the calculation for `minUsdAmountOut` and the corresponding expected output in the `toUsdAmount` function should be adjusted to account for the protocol's fee structure during swaps. Instead of assuming a direct 1:1 ratio between BOOST tokens and USD tokens, the implementation should consider the fee deductions to ensure that `minUsdAmountOut` reflects a realistic expected output.