Cheery Plastic Caribou

High

# Incorrect `minUsdRemove` value could cause `_unfarmBuyBurn` to revert.

### Summary

In [`SolidlyV2AMO.sol:372`](https://github.com/sherlock-audit/2024-10-axion/blob/main/liquidity-amo/contracts/SolidlyV2AMO.sol#L372), the `minUsdRemove` parameter is set to an incorrect value, which could cause `_unfarmBuyBurn(...)` to revert because `usdRemoved` could be smaller than `minUsdRemove` due to rounding during liquidity removal.

### Root Cause

1. When removing liquidity in `_unfarmBuyBurn(...)`, it is required that `usdRemoved >= minUsdRemove`. The resulted `usdRemoved` is calculated by `liquidity * usdReserve / totalLp`.
```solidity
// SolidlyV2AMO.sol:_unfarmBuyBurn(...)

        (boostRemoved, usdRemoved) = ISolidlyRouter(router).removeLiquidity(
            boost,
            usd,
            true,
            liquidity,
276:        minBoostRemove,
277:        minUsdRemove,
            address(this),
            deadline
        );
```
https://github.com/sherlock-audit/2024-10-axion/blob/main/liquidity-amo/contracts/SolidlyV2AMO.sol#L271-L280

```solidity
    function removeLiquidity(
        address tokenA,
        address tokenB,
        bool stable,
        uint liquidity,
        uint amountAMin,
        uint amountBMin,
        address to,
        uint deadline
    ) public ensure(deadline) returns (uint amountA, uint amountB) {
        address pair = pairFor(tokenA, tokenB, stable);
        require(IPair(pair).transferFrom(msg.sender, pair, liquidity), "Equalizer Router: transfer failed"); // send liquidity to pair
398:    (uint amount0, uint amount1) = IPair(pair).burn(to);
        (address token0,) = sortTokens(tokenA, tokenB);
        (amountA, amountB) = tokenA == token0 ? (amount0, amount1) : (amount1, amount0);
401:    require(amountA >= amountAMin, "Equalizer Router: INSUFFICIENT_A_AMOUNT");
402:    require(amountB >= amountBMin, "Equalizer Router: INSUFFICIENT_B_AMOUNT");
    }
```
(Equalizer Router v2: https://vscode.blockscan.com/fantom/0x2aa07920e4ecb4ea8c801d9dfece63875623b285)

```solidity
    function burn(address to) external lock returns (uint amount0, uint amount1) {
        (uint _reserve0, uint _reserve1) = (reserve0, reserve1);
        (address _token0, address _token1) = (token0, token1);
        uint _balance0 = IERC20(_token0).balanceOf(address(this));
        uint _balance1 = IERC20(_token1).balanceOf(address(this));
        uint _liquidity = balanceOf[address(this)];

        uint _totalSupply = totalSupply; // gas savings, must be defined here since totalSupply can update in _mintFee
833:    amount0 = _liquidity * _balance0 / _totalSupply; // using balances ensures pro-rata distribution
834:    amount1 = _liquidity * _balance1 / _totalSupply; // using balances ensures pro-rata distribution
        require(amount0 > 0 && amount1 > 0, "ILB"); // Pair: INSUFFICIENT_LIQUIDITY_BURNED
        _burn(address(this), _liquidity);
        _safeTransfer(_token0, to, amount0);
        _safeTransfer(_token1, to, amount1);
        _balance0 = IERC20(_token0).balanceOf(address(this));
        _balance1 = IERC20(_token1).balanceOf(address(this));

        _update(_balance0, _balance1, _reserve0, _reserve1);
        emit Burn(msg.sender, amount0, amount1, to);
    }
```
(Equalizer pool: https://vscode.blockscan.com/fantom/0x8db550677053f2ca3d3bf677cb4e06cc10511958)

2. The above `minUsdRemove` parameter is set to `toUsdAmount(usdNeeded)` in `_unfarmBuyBurn()`, and `liquidity = (usdNeeded * totalLp) / usdReserve`. 
```solidity
    function _unfarmBuyBurn() internal override returns (uint256 liquidity, uint256 newBoostPrice) {
        (uint256 boostReserve, uint256 usdReserve) = getReserves();

        uint256 usdNeeded = (((boostReserve - usdReserve) / 2) * usdBuyRatio) / FACTOR;
        uint256 totalLp = IERC20Upgradeable(pool).totalSupply();
363:    liquidity = (usdNeeded * totalLp) / usdReserve;
...
        _unfarmBuyBurn(
            liquidity,
            (liquidity * boostReserve) / totalLp, // the minBoostRemove argument
372:        toUsdAmount(usdNeeded), // the minUsdRemove argument
            usdNeeded, // the minBoostAmountOut argument
            block.timestamp + 1 // deadline is next block as the computation is valid instantly
        );

        newBoostPrice = boostPrice();
    }
```
https://github.com/sherlock-audit/2024-10-axion/blob/main/liquidity-amo/contracts/SolidlyV2AMO.sol#L358-L378

3. We need to perform two calculations to get `usdRemoved`, both of which may involve rounding down, which may lead to `usdRemoved < usdNeeded`, and revert the `_unfarmBuyBurn(...)`.
```solidity
usdRemoved = liquidity * usdReserve / totalLp
liquidity = (usdNeeded * totalLp) / usdReserve
=>
usdRemoved = ((usdNeeded * totalLp) / usdReserve) * usdReserve / totalLp <= toUsdAmount(usdNeeded)
```

### Internal pre-conditions

Rounding down in the above two calculations to obtain `usdRemoved` makes `usdRemoved < toUsdAmount(usdNeeded)`.

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

`_unfarmBuyBurn()` may not work when involving the above mentioned rounding down calculations. Due to the frequent occurrence of rounding down, `_unfarmBuyBurn()` may often not function properly, which will impact the rebalancing process of boost.

### PoC

Modify the "should execute public unfarmBuyBurn when price below 1" test case, and run it.
```solidity
363:  it("should execute public unfarmBuyBurn when price below 1", async function() {
364:-   const boostToBuy = ethers.parseUnits("1000000", 18);
    +   const boostToBuy = ethers.parseUnits("999000", 18);  // 1000000 => 999000
365:    const minUsdReceive = ethers.parseUnits("990000", 6);
```
https://github.com/sherlock-audit/2024-10-axion/blob/main/liquidity-amo/test/SolidlyV2AMO.test.ts#L363-L365

The result shows that `unfarmBuyBurn` reverts with `Router: INSUFFICIENT_B_AMOUNT` (see aforementioned Equalizer Router v2 code, L402).
```solidity
  1) SolidlyV2AMO
       should execute public unfarmBuyBurn when price below 1:
     Error: VM Exception while processing transaction: reverted with reason string 'Router: INSUFFICIENT_B_AMOUNT'
```

### Mitigation

```solidity
// SolidlyV2AMO.sol:_unfarmBuyBurn()

        _unfarmBuyBurn(
            liquidity,
            (liquidity * boostReserve) / totalLp, // the minBoostRemove argument
372: -      toUsdAmount(usdNeeded), // the minUsdRemove argument
     +      (liquidity * toUsdAmount(usdNeeded)) / totalLp,
            usdNeeded, // the minBoostAmountOut argument
            block.timestamp + 1 // deadline is next block as the computation is valid instantly
        );
```