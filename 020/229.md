Fast Lemonade Hawk

Medium

# `_addLiquidity()` may revert for SolidlyV2AMO due to killed gauge.


### Summary

When adding liquidity for SolidlyV2AMO, the LP tokens are automatically deposited into the Gauge contract. However, the deposit action may revert in some cases, which would cause the entire transaction to revert.

### Root Cause

When depositing LP tokens to the Gauge, `IGauge(gauge).deposit()` is called.

SolidlyV2 has a check that the gauge must be alive or else it would revert. `if (!IVoter(voter).isAlive(address(this))) revert NotAlive();`. The Governance of SolidlyV2 has the power to kill any gauge, and then depositing LP into that gauge would fail.

Since in SolidlyV2AMO, when adding liquidity, the LP is automatically deposited in the gauge, if the gauge is somehow killed, the add liquidity feature (both `addLiquidity()` and `mintSellFarm()` functions) would brick. Also, this cannot be fixed until the gauge is revived, because SolidlyV2AMO does not provide a setter function to set another gauge.

Note that a gauge being killed should only stop us from depositing LP tokens, but it should not brick adding liquidity, thus this is an issue.

SolidlyV2AMO.sol
```solidity
    function _addLiquidity(
        uint256 usdAmount,
        uint256 minBoostSpend,
        uint256 minUsdSpend,
        uint256 deadline
    ) internal override returns (uint256 boostSpent, uint256 usdSpent, uint256 liquidity) {
        ...

        // Approve the transfer of liquidity tokens to the gauge and deposit them
        IERC20Upgradeable(pool).approve(gauge, liquidity);
        if (useTokenId) {
@>          IGauge(gauge).deposit(liquidity, tokenId);
        } else {
@>          IGauge(gauge).deposit(liquidity);
        }

        // Burn excessive boosts
        if (boostAmount > boostSpent) IBoostStablecoin(boost).burn(boostAmount - boostSpent);

        emit AddLiquidityAndDeposit(boostSpent, usdSpent, liquidity, tokenId);
    }
```

I will provide the Aerodrome (SolidlyV2 fork) Gauge code for example.

Aerodrome/Gauge.sol
```solidity
    /// @inheritdoc IGauge
    function deposit(uint256 _amount) external {
        _depositFor(_amount, _msgSender());
    }

    function _depositFor(uint256 _amount, address _recipient) internal nonReentrant {
        if (_amount == 0) revert ZeroAmount();
@>      if (!IVoter(voter).isAlive(address(this))) revert NotAlive();

        address sender = _msgSender();
        _updateRewards(_recipient);

        IERC20(stakingToken).safeTransferFrom(sender, address(this), _amount);
        totalSupply += _amount;
        balanceOf[_recipient] += _amount;

        emit Deposit(sender, _recipient, _amount);
    }
```

Aerodrome/Voter.sol
```solidity
    /// @inheritdoc IVoter
    function killGauge(address _gauge) external {
        if (_msgSender() != emergencyCouncil) revert NotEmergencyCouncil();
        if (!isAlive[_gauge]) revert GaugeAlreadyKilled();
        // Return claimable back to minter
        uint256 _claimable = claimable[_gauge];
        if (_claimable > 0) {
            IERC20(rewardToken).safeTransfer(minter, _claimable);
            delete claimable[_gauge];
        }
        isAlive[_gauge] = false;
        emit GaugeKilled(_gauge);
    }

```

- https://github.com/sherlock-audit/2024-10-axion/blob/main/liquidity-amo/contracts/SolidlyV2AMO.sol#L240-L244
- https://github.com/aerodrome-finance/contracts/blob/main/contracts/gauges/Gauge.sol#L157
- https://github.com/aerodrome-finance/contracts/blob/main/contracts/Voter.sol#L381

### Internal pre-conditions

N/A

### External pre-conditions

1. Gauge for SolidlyV2AMO is killed.

### Attack Path

N/A

### Impact

The `addLiquidity()` function and `mintSellFarm()` will revert.

### PoC

N/A

### Mitigation

Two ways for mitigation:

1. Move the LP token deposit/withdraw logic to an independent function.
2. Add an option to toggle on/off whether to deposit the LP tokens.