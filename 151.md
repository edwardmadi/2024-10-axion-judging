Petite Strawberry Rabbit

Medium

# Minter can still mint when BoostStablecoin is paused

### Summary

A key security feature of the BoostStablecoin is the ability to pause. If `BoostStablecoin.sol` is in a paused state, `Minter` can still mint BOOST.

### Root Cause

Missing `whenNotPaused` modifier on `BoostStablecoin::mint()`.
https://github.com/sherlock-audit/2024-10-axion/blob/main/liquidity-amo/contracts/BoostStablecoin.sol#L51

### Internal pre-conditions

1. BoostStableCoint is in a paused state

### External pre-conditions

_No response_

### Attack Path

1. The `PAUSER` roles pauses `BoostStablecoin`
2. A user or the AMO calls `_mintAndSellBoost` (or any function where Minter is minting BOOST)
3. BOOST is minted while in paused state.

### Impact

If governance pauses BOOST and needs to stop all activity, this doesn't prevent minting new BOOST.

### PoC

To run POC:

1. Initialize a new Foundry project
2. Run `forge install https://github.com/OpenZeppelin/openzeppelin-contracts-upgradeable.git`
3. Create a file under /test/ and name is UsdtSecondApprove.t.sol
4. Paste the code below in the file
5. Run `forge test --mt testMintWhilePaused -vvvv`

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.19;

import "../lib/openzeppelin-contracts-upgradeable/contracts/token/ERC20/extensions/ERC20BurnableUpgradeable.sol";
import "../lib/openzeppelin-contracts-upgradeable/contracts/utils/PausableUpgradeable.sol";
import "../lib/openzeppelin-contracts-upgradeable/contracts/access/extensions/AccessControlEnumerableUpgradeable.sol";
import "../lib/openzeppelin-contracts-upgradeable/contracts/proxy/utils/Initializable.sol";
import "../lib/forge-std/src/Test.sol";

contract BoostStablecoin is
    Initializable,
    ERC20BurnableUpgradeable,
    PausableUpgradeable,
    AccessControlEnumerableUpgradeable
{
    bytes32 public constant PAUSER_ROLE = keccak256("PAUSER_ROLE");
    bytes32 public constant MINTER_ROLE = keccak256("MINTER_ROLE");

    function initialize(address admin) external initializer {
        __ERC20_init("Boost", "BOOST");
        __ERC20Burnable_init();
        __Pausable_init();
        __AccessControl_init();
        _grantRole(DEFAULT_ADMIN_ROLE, admin);
    }

    function pause() public onlyRole(PAUSER_ROLE) {
        _pause();
    }

    function mint(address to, uint256 amount) public onlyRole(MINTER_ROLE) {
        _mint(to, amount);
    }
}

contract Minter is Initializable, AccessControlEnumerableUpgradeable, PausableUpgradeable {

    address public boostAddress;
    bytes32 public constant AMO_ROLE = keccak256("AMO_ROLE");

    error NonContractSender();

    modifier onlyContract() {
        if (msg.sender.code.length == 0) revert NonContractSender();
        _;
    }

    function initialize(address boostAddress_) external initializer {
        __AccessControl_init();
        __Pausable_init();
        _grantRole(DEFAULT_ADMIN_ROLE, msg.sender);
        boostAddress = boostAddress_;
    }

    function protocolMint(address to, uint256 amount) external whenNotPaused onlyContract onlyRole(AMO_ROLE) {
        BoostStablecoin(boostAddress).mint(to, amount);
    }

}

contract Amo {}


contract MintWhilePausedTest is Test {

    address admin = makeAddr("admin");
    address pauser = makeAddr("pauser");
    BoostStablecoin boost;
    Minter minter;
    Amo amo;

    function setUp() public {
        vm.startPrank(admin);
        boost = new BoostStablecoin();
        minter = new Minter();
        amo = new Amo();
        boost.initialize(address(admin));
        minter.initialize(address(boost));
        vm.stopPrank();
    }

    function testMintWhilePaused() public {
        vm.startPrank(admin);
        boost.grantRole(boost.PAUSER_ROLE(), address(pauser));
        boost.grantRole(boost.MINTER_ROLE(), address(minter));
        minter.grantRole(minter.AMO_ROLE(), address(amo));
        vm.stopPrank();

        vm.startPrank(pauser);
        console.log("Pause state before pause():", boost.paused());
        boost.pause();
        console.log("Pause state after pause():", boost.paused());
        console.log("");
        vm.stopPrank();

        vm.startPrank(address(amo));
        console.log("Amo BOOST balance before mint: ", BoostStablecoin(boost).balanceOf(address(amo)));
        minter.protocolMint(address(amo), 1e18);
        console.log("Amo BOOST balance after mint: ", BoostStablecoin(boost).balanceOf(address(amo)));
    }
}
```

### Mitigation

Add whenNotPaused to `BoostStablecoin::mint()`