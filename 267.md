Cheery Cinnamon Rook

Medium

# The `SolidlyV2AMO`'s rebalancing operations can be bricked after integrating with some USD tokens on some EVM chains

### Summary

As per the readme, the `AXION` protocol's codebase would support any EVM chain:
> ***The smart contracts can potentially be implemented on any full-EVM chain.***

The protocol can integrate with any USD-pegged token (e.g., USDT, USDC, etc.). However, I discovered that some USD tokens on some EVM chains, such as USDT on Ethereum Mainnet, implement the so-called "*Approval Race Protections*" feature, which will break (/DoS) the protocol's `AMO` rebalancing operations. 

The vulnerability will cause the BOOST tokens pegged with the associated USD tokens in question (e.g., USDT) to depeg due to the permanent bricking of the `AMO` operations, eventually leading to the collapse of the BOOST token.

### Root Cause

Let's say the USD token considered in this report is [USDT on Ethereum Mainnet](https://etherscan.io/token/0xdac17f958d2ee523a2206206994597c13d831ec7#code). Other affected USD tokens should apply the same understanding.

Below presents the [`USDT::approve()`](https://etherscan.io/token/0xdac17f958d2ee523a2206206994597c13d831ec7#code) on Ethereum Mainnet. This function implements the so-called "*Approval Race Protections*" feature, which will revert a transaction if a caller is trying to approve an amount `M` (> 0) when an existing amount `N` (> 0) is already approved (see `@1` in the snippet below).

```solidity
    // Note, USDT::approve() on Ethereum Mainnet, https://etherscan.io/token/0xdac17f958d2ee523a2206206994597c13d831ec7#code
    function approve(address _spender, uint _value) public onlyPayloadSize(2 * 32) {

        //@audit @1 -- USDT on Ethereum Mainnet implementes the so-called "Approval Race Protections" feature,
        //             which will revert a transaction if a caller is trying to approve an amount M 
        //             (> 0) when an existing amount N (> 0) is already approved.
        //
        //
        // To change the approve amount you first have to reduce the addresses`
        //  allowance to zero by calling `approve(_spender, 0)` if it is not
        //  already 0 to mitigate the race condition described here:
        //  https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
@1      require(!((_value != 0) && (allowed[msg.sender][_spender] != 0)));

        allowed[msg.sender][_spender] = _value;
        Approval(msg.sender, _spender, _value);
    }
```
- `@1`: `USDT::approve()` on Ethereum Mainnet, https://etherscan.io/token/0xdac17f958d2ee523a2206206994597c13d831ec7#code

The root cause is that the `SolidlyV2AMO::_unfarmBuyBurn()` [approves the transfer of USD tokens](https://github.com/sherlock-audit/2024-10-axion/blob/d75df3636ea28dd627d548d5d473c5d5477b0dc6/liquidity-amo/contracts/SolidlyV2AMO.sol#L296) (i.e., USDT on Ethereum Mainnet, in this example) to the `router` to swap them for BOOST tokens (see `@2` below). However, the function does not revoke the approval after the swap.

After the swap, let's say just one wei (or more) of USDT is unspent, which is highly likely to happen due to possible price impact, price slippage, rounding issues, etc. This residual allowance amount can cause DoS to the `SolidlyV2AMO`'s rebalancing operations (i.e., TX reverted by the `USDT::approve()`), affecting both `SolidlyV2AMO::_unfarmBuyBurn()` and `SolidlyV2AMO::_addLiquidity()` in the following function calls.

```solidity
    function _unfarmBuyBurn(
        uint256 liquidity,
        uint256 minBoostRemove,
        uint256 minUsdRemove,
        uint256 minBoostAmountOut,
        uint256 deadline
    )
        internal
        override
        returns (uint256 boostRemoved, uint256 usdRemoved, uint256 usdAmountIn, uint256 boostAmountOut)
    {
        // Withdraw the specified amount of liquidity tokens from the gauge
        IGauge(gauge).withdraw(liquidity);

        // Approve the transfer of liquidity tokens to the router for removal
        IERC20Upgradeable(pool).approve(router, liquidity);

        uint256 usdBalanceBefore = balanceOfToken(usd);
        // Remove liquidity and store the amounts of USD and BOOST tokens received
        (boostRemoved, usdRemoved) = ISolidlyRouter(router).removeLiquidity(
            boost,
            usd,
            true,
            liquidity,
            minBoostRemove,
            minUsdRemove,
            address(this),
            deadline
        );
        uint256 usdBalanceAfter = balanceOfToken(usd);

        // we check that each USDC buys more than 1 BOOST (repegging is not an expense for the protocol)
        if (usdRemoved != usdBalanceAfter - usdBalanceBefore)
            revert UsdAmountOutMismatch(usdRemoved, usdBalanceAfter - usdBalanceBefore);

        // Ensure the BOOST amount is greater than or equal to the USD amount
        if ((boostRemoved * validRemovingRatio) / FACTOR < toBoostAmount(usdRemoved))
            revert InvalidRatioToRemoveLiquidity();

        // Define the route to swap USD tokens for BOOST tokens
        ISolidlyRouter.route[] memory routes = new ISolidlyRouter.route[](1);
        routes[0] = ISolidlyRouter.route(usd, boost, true);

        //@audit @2 -- The _unfarmBuyBurn() approves the transfer of USD tokens (e.g., USDT on Ethereum Mainnet) 
        //             to the router to swap them for BOOST tokens. However, the function does not revoke the approval 
        //             after the swap. 
        //
        //             After the swap, let's say just one wei (or more) of USDT is unspent. This residual allowance
        //             amount can cause DoS to the AMO rebalancing operations (i.e., TX reverted by the 
        //             USDT::approve()), affecting both _unfarmBuyBurn() and _addLiquidity() in the following calls.
        //
        // Approve the transfer of usd tokens to the router
@2      IERC20Upgradeable(usd).approve(router, usdRemoved);

        if (minBoostAmountOut < toBoostAmount(usdRemoved)) minBoostAmountOut = toBoostAmount(usdRemoved);

        // Execute the swap and store the amounts of tokens involved
        uint256[] memory amounts = ISolidlyRouter(router).swapExactTokensForTokens(
            usdRemoved,
            minBoostAmountOut,
            routes,
            address(this),
            deadline
        );

        // Burn the BOOST tokens received from the liquidity
        // Burn the BOOST tokens received from the swap
        usdAmountIn = amounts[0];
        boostAmountOut = amounts[1];
        IBoostStablecoin(boost).burn(boostRemoved + boostAmountOut);

        emit UnfarmBuyBurn(boostRemoved, usdRemoved, liquidity, boostAmountOut);
    }
```
- `@2`: https://github.com/sherlock-audit/2024-10-axion/blob/d75df3636ea28dd627d548d5d473c5d5477b0dc6/liquidity-amo/contracts/SolidlyV2AMO.sol#L296

After `@2`, the residual allowance amount will cause the [`USDT::approve()` to revert a transaction](https://github.com/sherlock-audit/2024-10-axion/blob/d75df3636ea28dd627d548d5d473c5d5477b0dc6/liquidity-amo/contracts/SolidlyV2AMO.sol#L214) (see `@3` below) and DoS the `SolidlyV2AMO`'s rebalancing operations.

As a result, the vulnerability will brick all `AMO` rebalancing functions:
- `SolidlyV2AMO::addLiquidity()`  -- `AMO_ROLE` access-restricted function
- `SolidlyV2AMO::mintSellFarm()`  -- `AMO_ROLE` access-restricted function
- `SolidlyV2AMO::unfarmBuyBurn()` -- `AMO_ROLE` access-restricted function
- `SolidlyV2AMO::mintSellFarm()`  -- `Public` function
- `SolidlyV2AMO::unfarmBuyBurn()` -- `Public` function

```solidity
    function _addLiquidity(
        uint256 usdAmount,
        uint256 minBoostSpend,
        uint256 minUsdSpend,
        uint256 deadline
    ) internal override returns (uint256 boostSpent, uint256 usdSpent, uint256 liquidity) {
        // We only add liquidity when price is withing range (close to $1)
        // Price needs to be in range: 1 +- validRangeRatio / 1e6 == factor +- validRangeRatio
        // if price is too high, we need to mint and sell more before we add liqudiity
        uint256 price = boostPrice();
        if (price <= FACTOR - validRangeWidth || price >= FACTOR + validRangeWidth) revert InvalidRatioToAddLiquidity();

        // Mint the specified amount of BOOST tokens
        uint256 boostAmount = (toBoostAmount(usdAmount) * boostMultiplier) / FACTOR;

        IMinter(boostMinter).protocolMint(address(this), boostAmount);

        // Approve the transfer of BOOST and USD tokens to the router
        IERC20Upgradeable(boost).approve(router, boostAmount);

        //@audit @3 -- After @2, the residual allowance amount will cause the USDT::approve() to revert
        //             a transaction and DoS the AMO rebalancing operations.
@3      IERC20Upgradeable(usd).approve(router, usdAmount);

        uint256 lpBalanceBefore = balanceOfToken(pool);
        // Add liquidity to the BOOST-USD pool
        (boostSpent, usdSpent, liquidity) = ISolidlyRouter(router).addLiquidity(
            boost,
            usd,
            true,
            boostAmount,
            usdAmount,
            minBoostSpend,
            minUsdSpend,
            address(this),
            deadline
        );
        uint256 lpBalanceAfter = balanceOfToken(pool);

        if (liquidity != lpBalanceAfter - lpBalanceBefore)
            revert LpAmountOutMismatch(liquidity, lpBalanceAfter - lpBalanceBefore);

        // Revoke approval from the router
        IERC20Upgradeable(boost).approve(router, 0);
        IERC20Upgradeable(usd).approve(router, 0);

        // Approve the transfer of liquidity tokens to the gauge and deposit them
        IERC20Upgradeable(pool).approve(gauge, liquidity);
        if (useTokenId) {
            IGauge(gauge).deposit(liquidity, tokenId);
        } else {
            IGauge(gauge).deposit(liquidity);
        }

        // Burn excessive boosts
        if (boostAmount > boostSpent) IBoostStablecoin(boost).burn(boostAmount - boostSpent);

        emit AddLiquidityAndDeposit(boostSpent, usdSpent, liquidity, tokenId);
    }
```
- `@3`: https://github.com/sherlock-audit/2024-10-axion/blob/d75df3636ea28dd627d548d5d473c5d5477b0dc6/liquidity-amo/contracts/SolidlyV2AMO.sol#L214

### Internal pre-conditions

A typical call to the `SolidlyV2AMO::_unfarmBuyBurn()` and just one wei (or more) of the approved USD tokens to the `router` (in this example, USDT on Ethereum Mainnet) is left unspent after the swap, which is highly likely to happen due to possible price impact, price slippage, rounding issues, etc.

### External pre-conditions

When the protocol integrates with any USD-pegged tokens that implement the so-called "*Approval Race Protections*" feature. For instance, [USDT on Ethereum Mainnet](https://etherscan.io/token/0xdac17f958d2ee523a2206206994597c13d831ec7#code) (refer to `@1` in the `Root Cause` section for more).

### Attack Path

1. A typical call to the `SolidlyV2AMO`'s external functions (can be both `AMO_ROLE` access-restricted functions and `public` functions, such as `unfarmBuyBurn()`) which will trigger the internal `_unfarmBuyBurn()`. As explained earlier, just one wei (or more) of the approved USD tokens to the `router` (in this example, USDT on Ethereum Mainnet) is left unspent after the swap, which is highly likely to happen due to possible price impact, price slippage, rounding issues, etc.
2. In the following function call to the internal `_unfarmBuyBurn()` or `_addLiquidity()`, the residual allowance amount will trigger the `USDT::approve()`'s *Approval Race Protections* feature to revert a transaction (refer to `@1` in the `Root Cause` section for more).
3. The triggered bug will permanently DoS the `SolidlyV2AMO`'s rebalancing operations.

### Impact

The vulnerability will brick all `AMO` rebalancing functions:
- `SolidlyV2AMO::addLiquidity()`  -- `AMO_ROLE` access-restricted function
- `SolidlyV2AMO::mintSellFarm()`  -- `AMO_ROLE` access-restricted function
- `SolidlyV2AMO::unfarmBuyBurn()` -- `AMO_ROLE` access-restricted function
- `SolidlyV2AMO::mintSellFarm()`  -- `Public` function
- `SolidlyV2AMO::unfarmBuyBurn()` -- `Public` function

This will cause the BOOST (pool) tokens pegged with the associated USD tokens in question to depeg due to the permanent bricking of the `AMO` rebalancing operations, eventually leading to the collapse of the BOOST token.

### PoC

-

### Mitigation

Two solutions:
1. Clear any residual allowance at the end of the swap call (in the `SolidlyV2AMO::_unfarmBuyBurn()`).
2. Or, apply the [`SafeERC20::forceApprove()`](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/72c152dc1c41f23d7c504e175f5b417fccc89426/contracts/token/ERC20/utils/SafeERC20.sol#L74) instead of the `approve()` (in the `SolidlyV2AMO::_unfarmBuyBurn()`).