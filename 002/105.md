Thankful Plum Pheasant

Medium

# Improper calculation order cause the serious precision loss

### Summary

The improper calculation order in SolidlyV3AMO will cause serious precision loss because of improper round down.

### Root Cause

In [SolidlyV3AMO:350](https://github.com/sherlock-audit/2024-10-axion/blob/main/liquidity-amo/contracts/SolidlyV3AMO.sol#L350), we try to calculate the boost token's price via below calculation:
In this calculation `sqrtPriceX96 ** 2 / Q96 ** 2` will be round down. When sqrtPriceX96 is in the range `1*Q96 ** 2  =< sqrtPriceX96 ** 2 < 2 * Q96 **2` , the boost price will keep the same value: `10 ** (boostDecimals - usdDecimals + PRICE_DECIMALS)`. 

```solidity
    function boostPrice() public view override returns (uint256 price) {
        (uint160 _sqrtPriceX96, , , ) = ISolidlyV3Pool(pool).slot0();
        uint256 sqrtPriceX96 = uint256(_sqrtPriceX96);
        if (boost < usd) {
            price = (10 ** (boostDecimals - usdDecimals + PRICE_DECIMALS) * sqrtPriceX96 ** 2) / Q96 ** 2;
        } else {
            if (sqrtPriceX96 >= Q96) {
@>                price = 10 ** (boostDecimals - usdDecimals + PRICE_DECIMALS) / (sqrtPriceX96 ** 2 / Q96 ** 2);
            } else {
                price = (10 ** (boostDecimals - usdDecimals + PRICE_DECIMALS) * Q96 ** 2) / sqrtPriceX96 ** 2;
            }
        }
    }
```

### Internal pre-conditions

1. USD token's decimal is the same as BOOST token's decimal.
2. USD is the token0, BOOST is the token1.
3. sqrtPriceX96 > Q96

### External pre-conditions

N/A

### Attack Path

When we meet the above internal pre-condition, the precision loss may cause one incorrect boost price.

### Impact
- When users or amo bot try to get boostPrice from boostPrice() interface, they may get the wrong result, which may lead to the wrong decision.
- Checks in AMO based on boostPrice may be invalid.

### PoC
```solidity
// SPDX-License-Identifier: GPL-3.0

pragma solidity >=0.7.0 <0.9.0;

contract BoostPriceTest {
    function boostPrice(uint160 _sqrtPriceX96) public pure returns (uint256 price) {
        uint256 sqrtPriceX96 = uint256(_sqrtPriceX96);
        uint8 boostDecimals = 18;
        uint8 usdDecimals = 18;
        uint8 PRICE_DECIMALS = 6;
        uint256 Q96 = 2 ** 96;
        address boost = 0xfaAddC93baf78e89DCf37bA67943E1bE8F37Bb8c;
        address usd = 0x22753E4264FDDc6181dc7cce468904A80a363E44;
        if (boost < usd) {
            price = (10 ** (boostDecimals - usdDecimals + PRICE_DECIMALS) * sqrtPriceX96 ** 2) / Q96 ** 2;
        } else {
            if (sqrtPriceX96 >= Q96) {
                price = 10 ** (boostDecimals - usdDecimals + PRICE_DECIMALS) / (sqrtPriceX96 ** 2 / Q96 ** 2);
            } else {
                price = (10 ** (boostDecimals - usdDecimals + PRICE_DECIMALS) * Q96 ** 2) / sqrtPriceX96 ** 2;
            }
        }
    }
} 
```
Run this case with different sqrtPriceX96:
Case 1: sqrtPriceX96 = Q96(79228162514264337593543950336), it means boost price equals usd price. The output is 1000000.
Case 2: Now sqrtPriceX96 increase from Q96(79228162514264337593543950336) to 1.4Q96(1.1091943e+29). In this case, it means that BOOST price drops and USD price increases, because the BOOST is token1 and USD is token0. The output is still 1000000.

### Mitigation
When we calculate the boost price, we need to follow multiple first and then divide.  In this case, if we `10 ** (boostDecimals - usdDecimals + PRICE_DECIMALS) * Q96 ** 2`, we might meet the overflow issue. Suggest using safeMath.