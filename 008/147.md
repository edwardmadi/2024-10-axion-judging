Petite Strawberry Rabbit

Medium

# Protocol incompatible with USDT due to lack of 0 approval

### Summary

To `approve()` a spender to spend USDT, the allowance value must be 0, otherwise, the call will revert. In `SolidlyV2Amo::_unfarmBuyBurn()`, the `router` is approved to spend the `usd` token, but the approval isn't reset back to 0. Any future call from `SolidlyV2Amo` approving `router` to spend the `usd` token will revert is `usd` token is USDT.

### Root Cause

In `SolidlyV2Amo::_unfarmBuyBurn()`, the router is approved to spend `usd` token, but the approval isn't set back to 0 and `forceApprove()` isn't used.
<https://github.com/sherlock-audit/2024-10-axion/blob/main/liquidity-amo/contracts/SolidlyV2AMO.sol#L296-L307>

### Internal pre-conditions

1. `usd` token is USDT

### External pre-conditions

1. A BOOST/USDT pair exists
2. BOOST is under peg

### Attack Path

1. A user or the AMO calls `unfarmBuyBurn()` for the first time for the BOOST/USDT pool
2. The router is given approval to spend `usdRemoved` to execute `ISolidlyRouter(router).swapExactTokensForTokens()`
3. The function finishes, but doesn't set the allowance back to 0
4. A user or the AMO calls `unfarmBuyBurn()` again and the call reverts because USDT requires the allowance to be 0 to approve a non-zero allowance to a spender. The same revert would happen for the same reason if `_addLiquidity` is called anywhere.

### Impact

Users and the AMO will be blocked from calling `unfarmBuyBurn()` and `_addLiquidity`, breaking protocol functionality.

### PoC

To run POC:
1. Initialize a new Foundry project
2. Run `forge install https://github.com/OpenZeppelin/openzeppelin-contracts.git`
3. Create a  file under `/test/` and name is `UsdtSecondApprove.t.sol`
4. Paste the code below in the file
5. Run `forge test --mt test_UsdtTypeTokenRevertOnSecondApprove -vvvv`

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.19;

import "../lib/forge-std/src/Test.sol";
import "../lib/openzeppelin-contracts/contracts/token/ERC20/ERC20.sol";

interface IERC20Upgradeable {
    event Transfer(address indexed from, address indexed to, uint256 value);
    event Approval(address indexed owner, address indexed spender, uint256 value);
    function totalSupply() external view returns (uint256);
    function balanceOf(address account) external view returns (uint256);
    function transfer(address to, uint256 amount) external returns (bool);
    function allowance(address owner, address spender) external view returns (uint256);
    function approve(address spender, uint256 amount) external returns (bool);
    function transferFrom(address from, address to, uint256 amount) external returns (bool);
}


contract UsdtTypeToken is ERC20 {
    uint8 public decimal;
    mapping (address => mapping (address => uint)) public allowed;

    constructor(string memory _name, string memory _symbol, uint8 _decimals) ERC20(_name, _symbol) {
        decimal = _decimals;
    }

    // USDT approve() does NOT return a bool, but I added one here to 
    // make demonstrating the root cause of this issue easier. I submitted
    // another issue to address USDT not returning a bool.
    function approve(address _spender, uint256 _value) public override returns (bool) {
        
        // this line in USDT is what 
        require(!((_value != 0) && (allowed[msg.sender][_spender] != 0)));
        allowed[msg.sender][_spender] = _value;
        return true;
    }
}

contract Router {}

contract SolidlyV2AMO {

    address usd;
    address router;
    uint256 usdRemoved;
    
    constructor(address _usd, address _router, uint256 _usdRemoved) {
        usd = _usd;
        router = _router;
        usdRemoved = _usdRemoved;
    }

    // I removed all code from _unfarmBuyBurn() except the USD related approve because
    // that is all that is required to demonstrate the root cause of this issue 
    function _unfarmBuyBurn() public {
        IERC20Upgradeable(usd).approve(router, usdRemoved);

        // Missing reset of app to 0 here,
        // which is the root cause of this issue
    }
}

contract UsdtTypeTokenSecondApproveReverts is Test {
    
    Router routerContract;
    UsdtTypeToken usdContract;
    SolidlyV2AMO solidlyV2AMO;


    function setUp() public {
        routerContract = new Router();
        usdContract = new UsdtTypeToken("USDT", "USDT", 6);
        solidlyV2AMO = new SolidlyV2AMO(address(usdContract), address(routerContract), 10e18);
    } 

    function test_UsdtTypeTokenRevertOnSecondApprove() public {
        vm.startPrank(address(solidlyV2AMO));
        solidlyV2AMO._unfarmBuyBurn();

        // _unfarmBuyBurn() will revert, but so will anytime _addLiquidity is called.
        vm.expectRevert();
        solidlyV2AMO._unfarmBuyBurn();
        vm.stopPrank();
    }
}
```

### Mitigation

Use SafeERC20's `forceApprove` instead of `approve`