Petite Strawberry Rabbit

Medium

# USDT incompatible with IERC20Upgradeable(usd).approve()

### Summary

The `IERC20Upgradeable` interface is used to `approve` spenders to spend `usd` token in multiple places in `SolidlyV2AMO` and `SolidlyV3AMO`. 

Here's one example:
https://github.com/sherlock-audit/2024-10-axion/blob/main/liquidity-amo/contracts/SolidlyV2AMO.sol#L214

If the `usd` token is USDT, anytime `approve` is called, the call it will revert because USDT doesn't return a boolean value.

### Root Cause

`IERC20Upgradeable(usd).approve()` is incompatible with USDT.


### Internal pre-conditions

1. `usd` token is USDT

### External pre-conditions

1. A pair exists for USDT/BOOST

### Attack Path

1. A user or the AMO calls any function containing `IERC20Upgradeable(usd).approve()` for a BOOST/USDT pair
2. The call reverts because USDT doesn't return a bool

### Impact

Users and the AMO will be blocked from calling any function containing `IERC20Upgradeable(usd).approve()`, breaking protocol functionality.

### PoC

To run POC:

1. Initialize a new Foundry project
2. Run `forge install https://github.com/OpenZeppelin/openzeppelin-contracts.git`
3. Create a file under /test/ and name is UsdtApproveReverts.t.sol
4. Paste the code below in the file
5. Run `forge test --mt test_UsdtTypeTokenReverts -vvvv`

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.19;

import "../lib/forge-std/src/Test.sol";
import "../lib/openzeppelin-contracts/contracts/token/ERC20/ERC20.sol";

interface IERC20Upgradeable {
    event Transfer(address indexed from, address indexed to, uint256 value);
    event Approval(address indexed owner, address indexed spender, uint256 value);
    function totalSupply() external view returns (uint256);
    function transfer(address to, uint256 amount) external returns (bool);
    function allowance(address owner, address spender) external view returns (uint256);
    function approve(address spender, uint256 amount) external returns (bool);
    function transferFrom(address from, address to, uint256 amount) external returns (bool);
}


contract UsdtTypeToken {
    mapping (address => mapping (address => uint)) public allowed;

    modifier onlyPayloadSize(uint size) {
        require(!(msg.data.length < size + 4));
        _;
    }

    // USDT's approve function does not return a bool: https://etherscan.io/token/0xdac17f958d2ee523a2206206994597c13d831ec7#code
    function approve(address _spender, uint _value) public onlyPayloadSize(2 * 32) {
        require(!((_value != 0) && (allowed[msg.sender][_spender] != 0)));
        allowed[msg.sender][_spender] = _value;
    }
}

contract Router {}

contract SolidlyV2AMO {

    address usd;
    address router;
    uint256 usdRemoved;
    
    constructor(address _usd, address _router, uint256 _usdRemoved) {
        usd = _usd;
        router = _router;
        usdRemoved = _usdRemoved;
    }

    // removed all code from _unfarmBuyBurn() except USD related approves
    function _unfarmBuyBurn() public {
        IERC20Upgradeable(usd).approve(router, usdRemoved);
    }
}

contract UsdtTypeTokenReverts is Test {
    
    Router routerContract;
    UsdtTypeToken usdContract;
    SolidlyV2AMO solidlyV2AMO;


    function setUp() public {
        routerContract = new Router();
        usdContract = new UsdtTypeToken();
        solidlyV2AMO = new SolidlyV2AMO(address(usdContract), address(routerContract), 10e18);
    } 

    function test_UsdtTypeTokenReverts() public {
        vm.startPrank(address(solidlyV2AMO));
        vm.expectRevert();
        solidlyV2AMO._unfarmBuyBurn();
        vm.stopPrank();
    }
}
```

### Mitigation

Use SafeERC20's `forceApprove` instead of `approve`