Nutty Tweed Puppy

High

# Initialization Vulnerability Leading to Denial of Service in SolidlyV2AMO Contract

### Summary

The `SolidlyV2AMO` contract suffers from a denial of service (DoS) vulnerability due to improper usage of the `initializer` modifier in its inheritance chain. The conflict arises when the contract's `initialize` function attempts to invoke the `initialize` function of its parent contract, `MasterAMO`, through `super.initialize(...)`. Since both contracts utilize the `initializer` modifier, the process fails, leaving the `SolidlyV2AMO` contract in an unusable state after deployment via a proxy. This effectively causes a denial of service, preventing any use of the contract.

### Root Cause

The vulnerability is caused by the interaction between the `initializer` modifier in both the `SolidlyV2AMO` and `MasterAMO` contracts. When the `initialize` function of `SolidlyV2AMO` is called, it first executes its own initialization logic, where the `initializer` modifier is invoked for the first time at [`SolidlyV2AMO.sol:79`](https://github.com/sherlock-audit/2024-10-axion/blob/main/liquidity-amo/contracts/SolidlyV2AMO.sol#L79). Subsequently, it attempts to call the `initialize` method of the parent contract, `MasterAMO`, using `super.initialize(...)` at [`SolidlyV2AMO.sol:82`](https://github.com/sherlock-audit/2024-10-axion/blob/main/liquidity-amo/contracts/SolidlyV2AMO.sol#L82).

At this point, the `initializer` modifier in `MasterAMO` is triggered again. Since it is designed to prevent a function from being called more than once, it detects that the contract has already been initialized (due to the initial call in `SolidlyV2AMO`) when it reaches the `MasterAMO` initialize function at [`MasterAMO.sol:96`](https://github.com/sherlock-audit/2024-10-axion/blob/main/liquidity-amo/contracts/MasterAMO.sol#L96). This leads to a failure at the check defined in the `initializer` modifier in `MasterAMO` at [`MasterAMO.sol:104`](https://github.com/sherlock-audit/2024-10-axion/blob/main/liquidity-amo/contracts/MasterAMO.sol#L104), which causes the entire initialization process to fail, leaving the contract in a non-functional state after deployment via a proxy.

This results in a denial of service (DoS) vulnerability, as the initialization can never be completed successfully. The underlying condition for the failure can be referenced in OpenZeppelin's `Initializable` contract at [`Initializable.sol:87`](https://github.com/OpenZeppelin/openzeppelin-contracts-upgradeable/blob/2d081f24cac1a867f6f73d512f2022e1fa987854/contracts/proxy/utils/Initializable.sol#L87).

--- 

### Internal pre-conditions

- The `SolidlyV2AMO` contract is deployed using a proxy contract.
- The `SolidlyV2AMO` contract inherits from `MasterAMO`.
- Both the `SolidlyV2AMO` and `MasterAMO` contracts contain `initialize` functions with the `initializer` modifier.

### External pre-conditions

- The deployment occurs via a proxy contract, where the proxy contract is expected to call the `initialize` function.
- An attacker or user attempts to deploy and initialize the `SolidlyV2AMO` contract through this proxy, triggering the initialization process.


### Attack Path

1. Deploy the `SolidlyV2AMO` contract behind a proxy.
2. Call the `initialize` function of `SolidlyV2AMO`.
3. The `initialize` function calls `super.initialize(...)`, which invokes `MasterAMO`'s `initialize` function.
4. The `initializer` modifier in `MasterAMO`'s `initialize` function detects that the contract has already been initialized (due to the previous call in `SolidlyV2AMO`), causing a reversion.

### Impact

- The `SolidlyV2AMO` contract cannot be properly initialized, leading to a complete denial of service.
- Key contract functionalities cannot be executed, halting operations related to Automated Market Operations (AMO).

### PoC

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

import {Test, console} from "forge-std/Test.sol";
import {MockERC20} from "./../src/mock/MockERC20.sol";
import {MockRouter} from "./../src/mock/MockRouter.sol";
import {BoostStablecoin} from "./../src/BoostStablecoin.sol";
import {Minter} from "../src/Minter.sol";
import {ProxyAdmin} from "@openzeppelin/contracts/proxy/transparent/ProxyAdmin.sol";
import {TransparentUpgradeableProxy} from "@openzeppelin/contracts/proxy/transparent/TransparentUpgradeableProxy.sol";
import {IFactory} from "./../src/interfaces/v2/IFactory.sol";
import {IPair} from "./../src/interfaces/v2/IPair.sol";
import {IV2Voter} from "./../src/interfaces/v2/IV2Voter.sol";
import {SolidlyV2AMO} from "./../src/SolidlyV2AMO.sol";

contract CounterTest is Test {
    MockERC20 public usdToken;
    BoostStablecoin public boostToken;
    address public adminForBoostToken = makeAddr("adminForBoostToken");
    address public boostMinter = makeAddr("boostMinter");
    address public treasury = makeAddr("treasury");
    address public rewardVault = makeAddr("rewardVault");
    Minter public minter_Contract;

    // Proxy for boost token
    ProxyAdmin public ProxyAdminForBoostToken;
    TransparentUpgradeableProxy public TUproxyForBoostToken;

    // Proxy for Minter
    ProxyAdmin public ProxyAdminForMinter_con;
    TransparentUpgradeableProxy public TUproxyForMinter_con;

    // Factory address of Equalizer DEX PairFactory
    IFactory public V2_Factor;
    IPair public pool_addr_boost_usd;

    // Create Gauge
    IV2Voter public V2_Voter;
    address public GuageAddress;

    // Deploy router
    MockRouter public V2_Router;
    address public WETH = 0x21be370D5312f44cB42ce377BC9b8a0cEF1A4C83;

    // SolidlyV2AMO
    SolidlyV2AMO public V2_AMO;
    ProxyAdmin public ProxyAdminForV2_AMO;
    TransparentUpgradeableProxy public TUproxyForV2_AMO;

    function setUp() public {
        // Deploy Mock ERC20 Token
        usdToken = new MockERC20("usd token", "usd", 6);
        
        // Deploy BoostStablecoin
        boostToken = new BoostStablecoin();
        ProxyAdminForBoostToken = new ProxyAdmin();
        TUproxyForBoostToken = new TransparentUpgradeableProxy(address(boostToken), address(ProxyAdminForBoostToken), "");
        BoostStablecoin(address(TUproxyForBoostToken)).initialize(adminForBoostToken);
          
        // Deploy Minter
        minter_Contract = new Minter();
        ProxyAdminForMinter_con = new ProxyAdmin();
        TUproxyForMinter_con = new TransparentUpgradeableProxy(address(minter_Contract), address(ProxyAdminForMinter_con), "");
        
        // Grant roles to the minter contract
        vm.startPrank(adminForBoostToken);
        BoostStablecoin(address(TUproxyForBoostToken)).grantRole(boostToken.MINTER_ROLE(), address(TUproxyForMinter_con)); 
        BoostStablecoin(address(TUproxyForBoostToken)).grantRole(boostToken.MINTER_ROLE(), address(boostMinter));     
        vm.stopPrank();

        // Initialize Minter
        Minter(address(TUproxyForMinter_con)).initialize(address(TUproxyForBoostToken), address(usdToken), adminForBoostToken);
          
        // Mint tokens for testing
        vm.startPrank(boostMinter);  
        BoostStablecoin(address(TUproxyForBoostToken)).mint(adminForBoostToken, 10000e18);
        usdToken.mint(adminForBoostToken, 10000e6);
        vm.stopPrank();
       
        // Setup Factory and Pair
        vm.startPrank(adminForBoostToken);
        V2_Factor = IFactory(0xc6366EFD0AF1d09171fe0EBF32c7943BB310832a);
        pool_addr_boost_usd = IPair(V2_Factor.createPair(address(TUproxyForBoostToken), address(usdToken), true));
        vm.stopPrank();
       
        // Create a Gauge
        V2_Voter = IV2Voter(0xE3D1A117dF7DCaC2eB0AC8219341bAd92f18dAC1);
        vm.startPrank(V2_Voter.governor());
        GuageAddress = V2_Voter.createGauge(address(pool_addr_boost_usd));
        vm.stopPrank();
       
        // Deploy Router
        vm.startPrank(adminForBoostToken);
        V2_Router = new MockRouter(address(V2_Factor), WETH);
        vm.stopPrank();

        // Deploy SolidlyV2AMO and its proxy
        V2_AMO = new SolidlyV2AMO();
        ProxyAdminForV2_AMO = new ProxyAdmin();
        TUproxyForV2_AMO = new TransparentUpgradeableProxy(address(V2_AMO), address(ProxyAdminForV2_AMO), "");
        
        // Grant AMO role to SolidlyV2AMO
        Minter(address(TUproxyForMinter_con)).grantRole(minter_Contract.AMO_ROLE(), address(TUproxyForV2_AMO)); 
    }

    // Test for initialization failure
    function test_initialization_fail() public {
        // Attempt to initialize SolidlyV2AMO through the proxy
        SolidlyV2AMO(address(TUproxyForV2_AMO)).initialize(
            adminForBoostToken,
            address(TUproxyForBoostToken), 
            address(usdToken), 
            address(TUproxyForMinter_con), 
            address(V2_Router), 
            GuageAddress, 
            rewardVault, 
            0, 
            false, 
            1100000, // boostMultiplier 1.1e6
            10000, // validRangeWidth 0.01e6
            1010000, // validRemovingRatio 1.01e6
            990000, // boostLowerPriceSell 0.99e6
            1010000, // boostUpperPriceBuy 1.01e6
            800000, // boostSellRatio 0.8e6
            800000 // usdBuyRatio 0.8e6
        );
    }
}
```
---

**Output (Detailed Log)**  

```text
    │   ├─ [28420] SolidlyV2AMO::initialize(adminForBoostToken: [0x3F1B0d5594E63E2a6074aC7d6B43EC738386E015], TransparentUpgradeableProxy: [0x5991A2dF15A8F6A256D3Ec51E99254Cd3fb576A9], MockERC20: [0x5615dEB798BB3E4dFa0139dFa1b3D433Cc23b72f], TransparentUpgradeableProxy: [0x1d1499e622D69689cdf9004d05Ec547d650Ff211], MockRouter: [0x318acdc36C9C725Ed416831dEDF5757Ebcd7F873], 0xD8376d6bE7071f20eB5B7780c6786D4e23EC20A9, rewardVault: [0x21c9A87cfe5065D29325E590BAF4eA7f29A4Bf3d], 0, false, 1100000 [1.1e6], 10000 [1e4], 1010000 [1.01e6], 990000 [9.9e5], 1010000 [1.01e6], 800000 [8e5], 800000 [8e5]) [delegatecall]
    │   │   ├─ [1226] MockRouter::pairFor(MockERC20: [0x5615dEB798BB3E4dFa0139dFa1b3D433Cc23b72f], TransparentUpgradeableProxy: [0x5991A2dF15A8F6A256D3Ec51E99254Cd3fb576A9], true) [staticcall]
    │   │   │   └─ ← [Return] 0xbF8a3A68e90a99A19FD87f1E1A611608EadD1264
    │   │   └─ ← [Revert] revert: Initializable: contract is already initialized
    │   └─ ← [Revert] revert: Initializable: contract is already initialized
    └─ ← [Revert] revert: Initializable: contract is already initialized
```

---



### Mitigation

1. **Remove `initializer` Modifier from `MasterAMO`:** This will prevent the conflict caused by calling the `initialize` function twice from the child contract.
   
2. **Make `MasterAMO.initialize()` Internal:** Restrict access to the `MasterAMO`'s `initialize` function so that it can only be called internally by derived contracts like `SolidlyV2AMO`.

3. **Consolidate Initialization in `SolidlyV2AMO`:** Ensure that the initialization logic is centralized in the `SolidlyV2AMO` contract, initializing all state variables in a single function call.